/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <jailbreak_core>
#include <hamsandwich>
#include <fakemeta>

#define PLUGIN "[JB] SHOP:OxHit"
#define AUTHOR "Natsheh"

#define TASK_MADDNESS	8765456

new user_has_redbull, ox_maddness;

#define has_ox_maddness(%1)	(ox_maddness & (1<<(%1-1)))
#define take_ox_maddness(%1)	(ox_maddness &= ~(1<<(%1-1)))
#define set_ox_maddness(%1)	(ox_maddness |= (1<<(%1-1)))

#define has_oxhit(%1)	(user_has_redbull & (1<<(%1-1)))
#define give_oxhit(%1)	(user_has_redbull |= (1<<(%1-1)))
#define take_oxhit(%1)	(user_has_redbull &= ~(1<<(%1-1)))

#if AMXX_VERSION_NUM > 182
#define client_disconnect client_disconnected
#endif

new g_item, HamHook:HAM_TOUCH_HOOK, laserbeam_sprite, pcvar_length, pcvar_oxhit_ff;

public plugin_precache()
{
	laserbeam_sprite = PRECACHE_SPRITE_I("sprites/laserbeam.spr")
	PRECACHE_SOUND("weapons/explode5.wav")
	PRECACHE_SOUND("weapons/explode4.wav")
	PRECACHE_SOUND("weapons/explode3.wav")
	PRECACHE_SOUND("jailbreak/angry_bull_sound.wav")
}

public plugin_init() {
	register_plugin(PLUGIN, VERSION, AUTHOR)
	
	pcvar_length = register_cvar("jb_oxhit_maddness_length", "3.0");
	pcvar_oxhit_ff = register_cvar("jb_oxhit_ff", "0");
	
	g_item = register_jailbreak_shopitem("Ox hit", "RedBull Get Horns as fuck!!!", 5000, TEAM_PRISONERS);
	
	HAM_TOUCH_HOOK = RegisterHam(Ham_Touch, "player", "fw_player_touch_post", true);
	DisableHamForward(HAM_TOUCH_HOOK)
	
	register_event("DeathMsg", "event_deathmsg", "a");
	
	register_clcmd("drop", "clcmd_drop");
}

public client_disconnect(id)
{
	if(has_ox_maddness(id))
	{
		remove_task(id+TASK_MADDNESS);
		take_ox_maddness(id);
		
		if(!ox_maddness) DisableHamForward(HAM_TOUCH_HOOK);
	}
	
	take_oxhit(id);
}

public event_deathmsg()
{
	new vic = read_data(2);
	
	if(has_ox_maddness(vic))
	{
		remove_task(vic+TASK_MADDNESS);
		set_pev(vic, pev_renderfx, kRenderFxNone);
		take_ox_maddness(vic);
		
		if(!ox_maddness) DisableHamForward(HAM_TOUCH_HOOK);
	}
	
	take_oxhit(vic);
}

public fw_player_touch_post(toucher, touched)
{
	if(is_user_alive(toucher) && has_ox_maddness(toucher))
	{
		if(is_user_alive(touched) && pev(touched, pev_takedamage) != DAMAGE_NO && (get_user_team(toucher) != get_user_team(touched) || get_pcvar_num(pcvar_oxhit_ff)))			
		{
			ExecuteHamB(Ham_Killed, touched, toucher, 2);
		}
	}
}

public clcmd_drop(id)
{
	if(has_oxhit(id))
	{
		activate_oxhit(id)
		return 1;
	}
	return 0;
}

public jb_shop_item_preselect(id, itemid)
{
	if(itemid == g_item) if(has_oxhit(id)) return JB_MENU_ITEM_UNAVAILABLE;
	return PLUGIN_CONTINUE;
}

public jb_shop_item_postselect(id, itemid)
{
	if(itemid == g_item) if(has_oxhit(id)) return JB_MENU_ITEM_UNAVAILABLE;
	return PLUGIN_CONTINUE;
}

public jb_shop_item_bought(id, itemid)
{
	if(g_item == itemid)
	{
		give_oxhit(id)
		cprint_chat(id, _, "press the drop key ^4'G' ^1to activate the oxhit!")
	}
}

public jb_day_started(iDayid)
{
	user_has_redbull = 0;
}

public jb_round_end()
{
	user_has_redbull = 0;
}

public jb_lr_duel_started(prisoner, guard, duelid)
{
	take_oxhit(prisoner);
	take_oxhit(guard);
}

activate_oxhit(id)
{
	if(!ox_maddness)
	{
		EnableHamForward(HAM_TOUCH_HOOK);
	}
	
	take_oxhit(id);
	set_ox_maddness(id);
	
	new iOrigin[3];
	get_user_origin(id, iOrigin)
	
	set_pev(id, pev_renderfx, kRenderFxGlowShell);
	set_pev(id, pev_rendercolor, Float:{200.0,000.0,000.0});
	set_pev(id, pev_renderamt, 50.0);
	
	message_begin(MSG_BROADCAST, SVC_TEMPENTITY);
	write_byte(TE_SPRITE);
	write_coord(iOrigin[0]);
	write_coord(iOrigin[1]);
	write_coord(iOrigin[2]);
	write_short(laserbeam_sprite);
	write_byte(30);
	write_byte(200);
	message_end();
	
	new iLen = clamp(get_pcvar_num(pcvar_length), 1, 5);
	
	message_begin(MSG_BROADCAST, SVC_TEMPENTITY);
	write_byte(TE_BEAMTORUS);
	write_coord(iOrigin[0]);
	write_coord(iOrigin[1]);
	write_coord(iOrigin[2]);
	write_coord(iOrigin[0]);
	write_coord(iOrigin[1]);
	write_coord(iOrigin[2]+100);
	write_short(laserbeam_sprite); 
	write_byte(0);
	write_byte(10);
	write_byte(30);
	write_byte(20);
	write_byte(10);
	write_byte(200);
	write_byte(20);
	write_byte(20);
	write_byte(200);
	write_byte(5);
	message_end();
	
	set_task(0.1, "task_velo", id);
	
	emit_sound(id, CHAN_BODY, "weapons/explode5.wav", VOL_NORM, ATTN_NORM, 0, PITCH_HIGH);
	emit_sound(id, CHAN_ITEM, "weapons/explode4.wav", VOL_NORM, ATTN_NORM, 0, PITCH_NORM);
	emit_sound(id, CHAN_VOICE, "weapons/explode3.wav", VOL_NORM, ATTN_NORM, 0, PITCH_LOW);
	
	set_task(float(iLen), "task_maddness_warnoff", id+TASK_MADDNESS);
}

public task_velo(id)
{
	new Float:fVelocity[3];
	velocity_by_aim(id, random_num(5000, 10000), fVelocity);
	set_pev(id, pev_velocity, fVelocity);
	
	emit_sound(id, CHAN_AUTO, "jailbreak/angry_bull_sound.wav", VOL_NORM,ATTN_NORM, 0, PITCH_HIGH)
}

public task_maddness_warnoff(taskid)
{
	new id = taskid - TASK_MADDNESS;
	
	if(!is_user_alive(id))
	{
		remove_task(taskid);
		return;
	}
	
	set_pev(id, pev_renderfx, kRenderFxNone);
	take_ox_maddness(id);
	
	if(!ox_maddness)
	{
		DisableHamForward(HAM_TOUCH_HOOK);
	}
	
	// dont know if this needed...
	remove_task(taskid);
}
