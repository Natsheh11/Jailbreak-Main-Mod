/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <fakemeta>
#include <jailbreak_core>
#include <cs_player_models_api>

#define PLUGIN "[JB] SHOP : Disguise"
#define AUTHOR "Natsheh"

#if AMXX_VERSION_NUM > 182
#define client_disconnect client_disconnected
#endif

enum any:MODEL_DATA(+=1)
{
	MODEL_NAME[MAX_CLASS_PMODEL_LENGTH],
	MODEL_BODY,
	MODEL_SKIN
}

new g_iItemID, Array:g_guards_skins, Array:g_prisoners_skins,
	g_iBought_Disguise, g_iInDisguise, g_szOLDMODEL[MAX_PLAYERS+1][MODEL_DATA];

public plugin_init()
{
	register_plugin(PLUGIN, VERSION, AUTHOR)
	
	g_guards_skins = ArrayCreate(MODEL_DATA, 1);
	g_prisoners_skins = ArrayCreate(MODEL_DATA, 1);
	
	g_iItemID = register_jailbreak_shopitem("Disguise", "Disguise in your enemy cloths!", 13000, TEAM_ANY);

	register_clcmd("buyequip", "disguise_toggle");
}

public jb_lr_duel_start(prisoner, guard, duelid)
{
	if(check_flag(g_iInDisguise,prisoner))
	{
		disguise_toggle(prisoner);
	}
	if(check_flag(g_iInDisguise,guard))
	{
		disguise_toggle(guard);
	}

	g_iBought_Disguise = g_iInDisguise = 0;
}

public jb_day_start()
{
	g_iBought_Disguise = g_iInDisguise = 0;
}

public jb_round_end()
{
	g_iBought_Disguise = g_iInDisguise = 0;
}

public disguise_toggle(id)
{
	if(!check_flag(g_iBought_Disguise,id))
	{
		return PLUGIN_CONTINUE;
	}

	if(!check_flag(g_iInDisguise,id))
	{
		if(!set_user_disguise(id))
		{
			return PLUGIN_CONTINUE;
		}

		set_flag(g_iInDisguise,id);
		cprint_chat(id, _, "You're now Disguised as your opponent!");
	}
	else
	{
		cs_set_player_model(id, g_szOLDMODEL[id][MODEL_NAME]);
		set_pev(id, pev_body, g_szOLDMODEL[id][MODEL_BODY]);
		set_pev(id, pev_skin, g_szOLDMODEL[id][MODEL_SKIN]);
		remove_flag(g_iInDisguise,id);
		cprint_chat(id, _, "You took off your Disguise !");
	}

	return PLUGIN_HANDLED;
}

public plugin_end()
{
	ArrayDestroy(g_guards_skins);
	ArrayDestroy(g_prisoners_skins);
}

public plugin_cfg()
{
	new i = jb_get_classes_count();
	
	new xArray[MODEL_DATA];
	for(new j, iFlags, iTeam; j < i; j++)
	{
		jb_get_classdata(j, .team=iTeam, .flags=iFlags, .model=xArray[MODEL_NAME], .mlen=charsmax(xArray[MODEL_NAME]), .modelbody=xArray[MODEL_BODY], .modelskin=xArray[MODEL_SKIN]);

		if(iFlags != 0)
		{
			continue;
		}
		
		switch( iTeam )
		{
			case TEAM_GUARDS:
			{
				ArrayPushArray(g_guards_skins, xArray);
			}
			case TEAM_PRISONERS:
			{
				ArrayPushArray(g_prisoners_skins, xArray);
			}
		}
	}
}

public jb_shop_item_preselect(id, itemid)
{
	if(itemid == g_iItemID)
	{
		if(check_flag(g_iBought_Disguise,id))
		{
			return JB_MENU_ITEM_UNAVAILABLE;
		}
	}
	
	return JB_IGNORED;
}

set_user_disguise(id)
{
	new xArray[MODEL_DATA], iSize;
	switch( get_user_team(id) )
	{
		case TEAM_GUARDS:
		{
			get_user_info(id, "model", g_szOLDMODEL[id][MODEL_NAME], charsmax(g_szOLDMODEL[][MODEL_NAME]));
			g_szOLDMODEL[id][MODEL_BODY] = pev(id, pev_body);
			g_szOLDMODEL[id][MODEL_SKIN] = pev(id, pev_skin);

			if((iSize=ArraySize(g_prisoners_skins)) > 0)
				ArrayGetArray(g_prisoners_skins, random(iSize), xArray);
			else
				copy(xArray[MODEL_NAME], charsmax(xArray[MODEL_NAME]), "terror");
			cs_set_player_model(id, xArray[MODEL_NAME]);
			set_pev(id, pev_body, xArray[MODEL_BODY]);
			set_pev(id, pev_skin, xArray[MODEL_SKIN]);
			return 1;
		}
		case TEAM_PRISONERS:
		{
			get_user_info(id, "model", g_szOLDMODEL[id][MODEL_NAME], charsmax(g_szOLDMODEL[][MODEL_NAME]));
			g_szOLDMODEL[id][MODEL_BODY] = pev(id, pev_body);
			g_szOLDMODEL[id][MODEL_SKIN] = pev(id, pev_skin);

			if((iSize=ArraySize(g_guards_skins)) > 0)
				ArrayGetArray(g_guards_skins, random(iSize), xArray);
			else
				copy(xArray[MODEL_NAME], charsmax(xArray[MODEL_NAME]), "gign");
			cs_set_player_model(id, xArray[MODEL_NAME]);
			set_pev(id, pev_body, xArray[MODEL_BODY]);
			set_pev(id, pev_skin, xArray[MODEL_SKIN]);
			return 1;
		}
	}

	return 0;
}

public jb_shop_item_bought(id, itemid)
{
	if(itemid == g_iItemID)
	{
		remove_flag(g_iInDisguise,id);
		set_flag(g_iBought_Disguise,id);
		cprint_chat(id, _, "toggle your !g'O' !ykey or the buyequip command to put on or take off your disguise!")
	}
}

public client_disconnect(id)
{
	remove_flag(g_iInDisguise,id);
	remove_flag(g_iBought_Disguise,id);
}
